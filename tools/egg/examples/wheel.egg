#
#  Example script for the EGG system, by Shawn Hargreaves.
#
#  A catherine-wheel effect. This uses two different types of particle:
#  as the wheel spins around, it creates a continual stream of sparks,
#  setting a suitable dx and dy heading vector for each one.


type spark
{
   size := 5;
   aa := 1;

   r := 0;
   g := 255;
   b := 0;
   a := 255;

   x = x+dx;
   y = y+dy;

   dy = dy+0.05;

   size = size+1.5;

   g = g-8;
   b = b+8;

   a = a-8;

   if (a <= 0) {
      die;
   }
}


type wheel
{
   size := 5;

   r := 0;
   g := 255;
   b := 0;

   dir := 0;

   lay spark {
      dx = cos(_dir)*1.5;
      dy = sin(_dir)*1.5;
   }

   dir = dir+16;
}


lay wheel;
